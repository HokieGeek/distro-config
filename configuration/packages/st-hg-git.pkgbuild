# Maintainer: Andres Perez <andres.f.perez@gmail.com>

pkgname=st-hg-git
_pkgname=st
epoch=1
pkgver=20160309.3996461
pkgrel=1
pkgdesc='Simple virtual terminal emulator for X'
url='http://git.suckless.org/st/'
arch=('i686' 'x86_64')
license=('MIT')
depends=('libxft')
makedepends=('ncurses' 'libxext' 'git')
source=('git://git.suckless.org/st')
sha1sums=('SKIP')

provides=("${_pkgname}")
conflicts=("${_pkgname}")

pkgver() {
    cd "${srcdir}/${_pkgname}"
    git log -1 --format='%cd.%h' --date=short | tr -d -
}

prepare() {
    local file
    pushd "${srcdir}/${_pkgname}"

    sed \
        -e 's/CPPFLAGS =/CPPFLAGS +=/g' \
        -e 's/CFLAGS =/CFLAGS +=/g' \
        -e 's/LDFLAGS =/LDFLAGS +=/g' \
        -i config.mk
    sed \
        -e '/char font/s/".*"/"DejaVu Sans Mono:size=12:antialias=true:autohint=true"/' \
        -e '/char worddelimiters/s/= .*/= " '"'"'`\\\"()[]{}<>|";/' \
        -i config.def.h

    echo "> Applying patches"
    curl -O "http://st.suckless.org/patches/st-git-20160131-argbbg.diff"
    curl -O "http://st.suckless.org/patches/st-git-20150917-hidecursor.diff"

    for file in `ls *`; do
        if [[ "${file}" == *.diff ]]; then
            # add all patches present in source directory
            echo "> Applying patch: ${file}"
            patch -Np1 < "${file}"
        fi
    done
}

build() {
    pushd "${srcdir}/${_pkgname}"
    make X11INC=/usr/include/X11 X11LIB=/usr/lib/X11
}

package() {
    pushd "${srcdir}/${_pkgname}"
    export TERMINFO="${pkgdir}/usr/share/terminfo"
    install -d "$TERMINFO"
    make PREFIX=/usr DESTDIR="${pkgdir}" install
    install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
    install -Dm644 README "${pkgdir}/usr/share/doc/${pkgname}/README"
}
