# Maintainer: Andres Perez <andres.f.perez@gmail.com>

pkgname=st-tmux-git
_pkgname=st
epoch=1
pkgver=0
pkgrel=1
pkgdesc='Simple virtual terminal emulator for X'
url='http://git.suckless.org/st/'
arch=('i686' 'x86_64')
license=('MIT')
depends=('libxft')
makedepends=('ncurses' 'libxext' 'git')
source=('git://git.suckless.org/st')
sha1sums=('SKIP')

provides=("${_pkgname}")
conflicts=("${_pkgname}")

pkgver() {
    cd "${srcdir}/${_pkgname}"
    git log -1 --format='%cd.%h' --date=short | tr -d -
}

# downloadPatches() {
#     cd "${srcdir}"
#     curl -O "http://st.suckless.org/patches/st-git-20160131-argbbg.diff"
# }

prepare() {
    local file
    cd "${srcdir}/${_pkgname}"
    sed \
        -e 's/CPPFLAGS =/CPPFLAGS +=/g' \
        -e 's/CFLAGS =/CFLAGS +=/g' \
        -e 's/LDFLAGS =/LDFLAGS +=/g' \
        -i config.mk
    sed \
        -e '/char font/s/".*"/"DejaVu Sans Mono:size=12:antialias=true:autohint=true"/' \
        -e '/char worddelimiters/s/= .*/= " '"'"'`\\\"()[]{}<>|";/' \
        -e '/char shell/s;".*";"/usr/bin/tmux";' \
        -i config.def.h

    # Terminfo entries are conflicting with ncurses
    sed -i '/@tic/d' Makefile

    for file in "${source[@]}"; do
        if [[ "$file" == *.diff ]]; then
            # add all patches present in source directory
            patch -Np1 <"$srcdir/$file"
        fi
    done
}

build() {
    cd "${srcdir}/${_pkgname}"
    make X11INC=/usr/include/X11 X11LIB=/usr/lib/X11
}

package() {
    cd "${srcdir}/${_pkgname}"
    # export TERMINFO="${pkgdir}/usr/share/terminfo"
    # install -d "$TERMINFO"
    make PREFIX=/usr DESTDIR="${pkgdir}" install
    install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
    install -Dm644 README "${pkgdir}/usr/share/doc/${pkgname}/README"
}
